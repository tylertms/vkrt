#version 460
#extension GL_EXT_ray_tracing : enable
#include "utility/common.glsl"
#include "bsdf/eval.glsl"

layout(location = 0) rayPayloadEXT Payload payload;

layout(set = 0, binding = 0) uniform accelerationStructureEXT topLevelAS;
layout(set = 0, binding = 1, rgba32f) uniform image2D image;

layout(set = 0, binding = 4) uniform SceneUniform {
    mat4 viewInverse;
    mat4 projInverse;
    uint frameNumber;
    uvec4 viewportRect;
} scene;

void main()  {
    ivec2 pixel = ivec2(gl_LaunchIDEXT.xy);
    ivec2 viewportOrigin = ivec2(scene.viewportRect.xy);
    ivec2 viewportSize = ivec2(scene.viewportRect.zw);
    uint state =  (pixel.y * 814793u + pixel.x * 27u) ^ (scene.frameNumber * 102943u) ^ scene.frameNumber;

    if (viewportSize.x <= 0 || viewportSize.y <= 0) {
        imageStore(image, pixel, vec4(0.0, 0.0, 0.0, 0.0));
        return;
    }

    ivec2 viewportMax = viewportOrigin + viewportSize;
    if (pixel.x < viewportOrigin.x || pixel.y < viewportOrigin.y || pixel.x >= viewportMax.x || pixel.y >= viewportMax.y) {
        imageStore(image, pixel, vec4(0.0, 0.0, 0.0, 0.0));
        return;
    }

    vec2 viewportPixel = vec2(pixel - viewportOrigin) + vec2(0.5);
    vec2 inUV = viewportPixel / vec2(viewportSize);
    vec2 d = inUV * 2.0 - 1.0;

    Ray ray;
    ray.origin = (scene.viewInverse * vec4(0.0, 0.0, 0.0, 1.0)).xyz;

    vec4 viewDir = scene.projInverse * vec4(d.x, d.y, 1.0, 1.0);
    ray.dir = normalize((scene.viewInverse * vec4(viewDir.xyz, 0.0)).xyz);

    vec3 throughput = vec3(1.0);
    vec3 radiance = vec3(0.0);

    for (uint i = 0; i < MAX_BOUNCES; i++) {
        traceRayEXT(topLevelAS, gl_RayFlagsOpaqueEXT, 0xFF, 0, 0, 0, ray.origin, 0.001, ray.dir, 10000.0, 0);
        if (!payload.didHit) break;
        evalBSDF(payload, ray, radiance, throughput, state);
    }

    imageStore(image, pixel, vec4(radiance, 0.0));
}
