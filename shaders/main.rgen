#version 460
#extension GL_EXT_ray_tracing : enable
#include "utility/bindings.glsl"
#include "bsdf/eval.glsl"

layout(location = 0) rayPayloadEXT Payload payload;

vec3 traceSample(ivec2 pixel, inout uint state) {
    ivec2 viewportOrigin = ivec2(scene.viewportRect.xy);
    ivec2 viewportSize = ivec2(scene.viewportRect.zw);

    vec2 jitter = vec2(rand(state), rand(state));
    vec2 viewportPixel = vec2(pixel - viewportOrigin) + jitter;
    vec2 inUV = viewportPixel / vec2(viewportSize);
    vec2 d = inUV * 2.0 - 1.0;

    Ray ray;
    ray.origin = (scene.viewInverse * vec4(0.0, 0.0, 0.0, 1.0)).xyz;

    vec4 viewDir = scene.projInverse * vec4(d.x, d.y, 1.0, 1.0);
    ray.dir = normalize((scene.viewInverse * vec4(viewDir.xyz, 0.0)).xyz);

    vec3 throughput = vec3(1.0);
    vec3 radiance = vec3(0.0);

    payload.time = 0;
    bool timeLimitEnabled = scene.timeBase >= 0.0;
    float timeMax = scene.timeStep;

    for (uint i = 0; i < max(scene.maxBounces, 1u); i++) {
        payload.didHit = false;
        traceRayEXT(topLevelAS, gl_RayFlagsOpaqueEXT, 0xFF, 0, 0, 0, ray.origin, 0.001, ray.dir, 10000.0, 0);

        if (!payload.didHit) break;
        if (timeLimitEnabled && payload.time > timeMax) break;

        Material material = materialBuffer.materials[payload.materialIndex];
        radiance += throughput * (material.emissionColor * material.emissionStrength);

        evalBSDF(payload, material, ray, throughput, state);
    }

    return radiance;
}

void main() {
    ivec2 pixel = ivec2(gl_LaunchIDEXT.xy);
    ivec2 viewportOrigin = ivec2(scene.viewportRect.xy);
    ivec2 viewportSize = ivec2(scene.viewportRect.zw);

    if (viewportSize.x <= 0 || viewportSize.y <= 0) {
        imageStore(accumulationWriteImage, pixel, vec4(0.0));
        imageStore(outputImage, pixel, vec4(0.0));
        return;
    }

    ivec2 viewportMax = viewportOrigin + viewportSize;
    if (pixel.x < viewportOrigin.x || pixel.y < viewportOrigin.y || pixel.x >= viewportMax.x || pixel.y >= viewportMax.y) {
        imageStore(accumulationWriteImage, pixel, vec4(0.0));
        imageStore(outputImage, pixel, vec4(0.0));
        return;
    }

    uint spp = max(scene.samplesPerPixel, 1u);
    vec4 previous = imageLoad(accumulationReadImage, pixel);
    uint previousSamples = uint(previous.a + 0.5);
    uint rngBase = initRngBase(uvec2(pixel), scene.frameNumber);

    vec3 frameRadiance = vec3(0.0);
    for (uint sampleIndex = 0; sampleIndex < spp; sampleIndex++) {
        uint sampleNumber = previousSamples + sampleIndex;
        uint state = initRngState(rngBase, sampleNumber);
        frameRadiance += traceSample(pixel, state);
    }
    frameRadiance /= float(spp);

    float previousWeight = float(previousSamples);
    float newWeight = float(spp);
    vec3 accumulated = (previous.rgb * previousWeight + frameRadiance * newWeight) / (previousWeight + newWeight);

    vec3 mapped = accumulated;
    if (scene.toneMappingMode == 1u) {
        mapped = toneMapACES(mapped);
    }

    imageStore(accumulationWriteImage, pixel, vec4(accumulated, previousWeight + newWeight));
    imageStore(outputImage, pixel, vec4(mapped, 1.0));
}
