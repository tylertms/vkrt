#version 460
#extension GL_EXT_ray_tracing : enable
#include "utility/common.glsl"
#include "bsdf/eval.glsl"

layout(location = 0) rayPayloadEXT Payload payload;

layout(set = 0, binding = 0) uniform accelerationStructureEXT topLevelAS;
layout(set = 0, binding = 1, rgba32f) uniform image2D accumulationReadImage;
layout(set = 0, binding = 2, rgba32f) uniform image2D accumulationWriteImage;
layout(set = 0, binding = 3, rgba32f) uniform image2D outputImage;

layout(set = 0, binding = 6) uniform SceneUniform {
    mat4 viewInverse;
    mat4 projInverse;
    uint frameNumber;
    uint samplesPerPixel;
    uint maxBounces;
    uint toneMappingMode;
    uvec4 viewportRect;
} scene;

layout(set = 0, binding = 8, std430) readonly buffer MaterialBuffer {
    Material materials[];
} materialBuffer;

vec3 traceSample(ivec2 pixel, inout uint state) {
    ivec2 viewportOrigin = ivec2(scene.viewportRect.xy);
    ivec2 viewportSize = ivec2(scene.viewportRect.zw);

    vec2 jitter = vec2(rand(state), rand(state));
    vec2 viewportPixel = vec2(pixel - viewportOrigin) + jitter;
    vec2 inUV = viewportPixel / vec2(viewportSize);
    vec2 d = inUV * 2.0 - 1.0;

    Ray ray;
    ray.origin = (scene.viewInverse * vec4(0.0, 0.0, 0.0, 1.0)).xyz;

    vec4 viewDir = scene.projInverse * vec4(d.x, d.y, 1.0, 1.0);
    ray.dir = normalize((scene.viewInverse * vec4(viewDir.xyz, 0.0)).xyz);

    vec3 throughput = vec3(1.0);
    vec3 radiance = vec3(0.0);

    for (uint i = 0; i < max(scene.maxBounces, 1u); i++) {
        payload.didHit = false;
        traceRayEXT(topLevelAS, gl_RayFlagsOpaqueEXT, 0xFF, 0, 0, 0, ray.origin, 0.001, ray.dir, 10000.0, 0);
        if (!payload.didHit) {
            break;
        }
        Material material = materialBuffer.materials[payload.materialIndex];
        evalBSDF(payload, material, ray, radiance, throughput, state);
    }

    return radiance;
}

void main() {
    ivec2 pixel = ivec2(gl_LaunchIDEXT.xy);
    ivec2 viewportOrigin = ivec2(scene.viewportRect.xy);
    ivec2 viewportSize = ivec2(scene.viewportRect.zw);

    if (viewportSize.x <= 0 || viewportSize.y <= 0) {
        imageStore(accumulationWriteImage, pixel, vec4(0.0));
        imageStore(outputImage, pixel, vec4(0.0));
        return;
    }

    ivec2 viewportMax = viewportOrigin + viewportSize;
    if (pixel.x < viewportOrigin.x || pixel.y < viewportOrigin.y || pixel.x >= viewportMax.x || pixel.y >= viewportMax.y) {
        imageStore(accumulationWriteImage, pixel, vec4(0.0));
        imageStore(outputImage, pixel, vec4(0.0));
        return;
    }

    uint spp = max(scene.samplesPerPixel, 1u);
    vec4 previous = imageLoad(accumulationReadImage, pixel);
    uint previousSamples = uint(previous.a);
    uint state = (pixel.y * 814793u + pixel.x * 27u) ^ ((scene.frameNumber + 1u) * 102943u) ^ ((previousSamples + 1u) * 19349663u);

    vec3 frameRadiance = vec3(0.0);
    for (uint sampleIndex = 0; sampleIndex < spp; sampleIndex++) {
        frameRadiance += traceSample(pixel, state);
    }
    frameRadiance /= float(spp);

    float previousWeight = float(previousSamples);
    float newWeight = float(spp);
    vec3 accumulated = (previous.rgb * previousWeight + frameRadiance * newWeight) / (previousWeight + newWeight);

    vec3 mapped = accumulated;
    if (scene.toneMappingMode == 1u) {
        mapped = toneMapACES(mapped);
    }

    imageStore(accumulationWriteImage, pixel, vec4(accumulated, previousWeight + newWeight));
    imageStore(outputImage, pixel, vec4(mapped, 1.0));
}
